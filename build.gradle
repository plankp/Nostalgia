plugins {
    id 'java'
    id 'application'
}

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

application {
    mainClassName = 'org.atoiks.games.nostalgia.App'
}

task createExtraAppDummy(type: CreateStartScripts) {
    mainClassName = 'org.atoiks.games.nostalgia.toolchain.Dummy'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    applicationName = 'dummy'
}

task createExtraAppLegacyAssembler(type: CreateStartScripts) {
    mainClassName = 'org.atoiks.games.nostalgia.toolchain.LegacyAssembler'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    applicationName = 'as'
}

applicationDistribution.into("bin") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(createExtraAppDummy)
    from(createExtraAppLegacyAssembler)
    fileMode = 0755
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}